/*
 * build.gradle
 *
 * Copyright (c) 2015. Joe Nellis
 * Distributed under MIT License. See accompanying file License.txt or at
 * http://opensource.org/licenses/MIT
 */

plugins {
  id "com.jfrog.bintray" version "1.8.4"
  id "me.champeau.gradle.jmh" version "0.4.3"
  id "io.morethan.jmhreport" version "0.6.2.1" 
}
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'groovy'
apply from: 'bintray.gradle'

compileJava.options.compilerArgs = ["-Xlint:unchecked"]
project.version = "1.2"

repositories {                       
  mavenCentral()
}

dependencies {
  testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
  testCompile "junit:junit:4.12"
}

javadoc{
  source = sourceSets.main.allJava
  options.overview = "${project.projectDir}/src/main/java/net/jnellis/binpack/overview.html"
  options.noQualifiers("java.lang")
  options.links("https://docs.oracle.com/javase/8/docs/api/")
  destinationDir = project.file("${project.projectDir}/docs/javadocs")
}

def reportFile = project.file("${project.projectDir}/docs/jmh/report.json")

//https://github.com/melix/jmh-gradle-plugin
jmh {
  //include = [".*BestFitC.*"]
  iterations = 10
  warmupIterations = 10
  jmhVersion = '1.19'
  benchmarkMode = ['avgt']
  resultFormat = 'JSON'
  resultsFile = reportFile
  profilers=[]
  fork = 2
  timeUnit ='us'
}

jmhReport {
  jmhResultPath = reportFile
  jmhReportOutput = project.file("${project.projectDir}/docs/jmh")
}

tasks.jmh.finalizedBy tasks.jmhReport